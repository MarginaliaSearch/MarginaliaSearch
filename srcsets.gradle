/** Configures the source sets for the code/ subprojects.
 *
 * Ideally this would have been done in the root build.gradle file, but due to disagreements
 * between Gradle and IntelliJ's gradle plugin about how to interpret the sourceSets block
 * when applied to subprojects from the root project, this has to be done in each subproject.
 * */
apply plugin: 'java'
apply plugin: 'com.adarshr.test-logger'
apply plugin: 'com.gradleup.shadow'
apply plugin: 'application'

test {
    maxHeapSize = "8G"
    useJUnitPlatform()
}

tasks.register('fastTests', Test) {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    maxHeapSize = "8G"
    useJUnitPlatform {
        excludeTags("slow", "flaky")
    }
}

if (project.plugins.hasPlugin('application')) {
    if (!application.mainClass.isPresent() || !application.applicationName.endsWith("-service")) {
        tasks.shadowJar.enabled = false
    }
    else {
    }


    shadowJar {
        mergeServiceFiles()

        archiveBaseName = application.applicationName
        archiveClassifier = ''
        archiveVersion = project.version
        zip64 true
        // Manifest for executable JAR
        manifest {
            attributes(
                    'Main-Class': application.mainClass,
                    'Implementation-Version': project.version
            )
        }

        // Minimize JAR size by excluding unnecessary files
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
    }

}

dependencies {
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
sourceSets {
    main {
        java {
            srcDirs = [
                    'java',
                    'build/generated/source/proto/main/grpc',
                    'build/generated/source/proto/main/java'
                    ]
        }
        resources {
            srcDirs = [ 'resources' ]
        }
    }
    test {
        java {
            srcDirs = [ 'test' ]
        }
        resources {
            srcDirs = [ 'test-resources' ]
        }
    }
}
