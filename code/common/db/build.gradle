plugins {
    id 'java'
    id "io.freefair.lombok" version "5.3.3.3"
    id 'jvm-test-suite'
    id "org.flywaydb.flyway" version "8.2.0"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(20))
    }
}

configurations {
    flywayMigration.extendsFrom(implementation)
}

dependencies {
    implementation project(':code:common:model')

    implementation libs.lombok
    annotationProcessor libs.lombok
    implementation libs.bundles.slf4j

    implementation libs.guice
    implementation libs.bundles.gson

    implementation libs.notnull

    implementation libs.commons.lang3

    implementation libs.trove

    implementation libs.rxjava
    implementation libs.bundles.mariadb
    flywayMigration 'org.flywaydb:flyway-mysql:9.8.1'

    testImplementation libs.bundles.slf4j.test
    testImplementation libs.bundles.junit
    testImplementation libs.mockito


    testImplementation platform('org.testcontainers:testcontainers-bom:1.17.4')
    testImplementation 'org.testcontainers:mariadb:1.17.4'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.4'
}

flyway {
    url = 'jdbc:mariadb://localhost:3306/WMSA_prod'
    user = 'wmsa'
    password = 'wmsa'
    schemas = ['WMSA_prod']
    configurations = [ 'compileClasspath', 'flywayMigration' ]
    locations = ['filesystem:src/main/resources/db/migration']
}


test {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    maxHeapSize = "8G"
    useJUnitPlatform()
}

task fastTests(type: Test) {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    maxHeapSize = "8G"
    useJUnitPlatform {
        excludeTags "slow"
    }
}
