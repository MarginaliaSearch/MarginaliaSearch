plugins {
    id 'java'

    id 'application'
    id 'com.gradleup.shadow'
    id 'jvm-test-suite'
    id 'com.google.cloud.tools.jib' version '3.4.5'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(rootProject.ext.jvmVersion))
    }
}

application {
    mainClass = 'nu.marginalia.api.ApiMain'
    applicationName = 'api-service'
}

tasks.distZip.enabled = false

apply from: "$rootProject.projectDir/srcsets.gradle"
apply from: "$rootProject.projectDir/docker.gradle"

dependencies {
    implementation project(':code:common:db')
    implementation project(':code:common:model')
    implementation project(':code:common:service')
    implementation project(':code:common:config')
    implementation project(':code:functions:search-query:api')
    implementation project(':code:functions:search-query')

    implementation libs.bundles.slf4j

    libs.bundles.jooby.get().each {
        implementation dependencies.create(it) {
            // Jooby pulls in an incompatible slf4j version that breaks all logs
            exclude group: 'org.slf4j'
        }
    }
    implementation libs.prometheus
    implementation libs.notnull
    implementation libs.guava
    implementation dependencies.create(libs.guice.get()) {
        exclude group: 'com.google.guava'
    }

    implementation libs.opencsv
    implementation libs.trove
    implementation libs.protobuf
    implementation libs.fastutil
    implementation libs.bundles.gson
    implementation libs.bundles.mariadb
    testImplementation libs.bundles.slf4j.test
    testImplementation libs.bundles.junit
    testImplementation libs.bundles.grpc
    testImplementation libs.mockito
    testImplementation platform('org.testcontainers:testcontainers-bom:1.21.4')
    testImplementation libs.commons.codec
    testImplementation 'io.jooby:jooby-test:3.11.5'
    testImplementation 'org.testcontainers:mariadb:1.21.4'
    testImplementation 'org.testcontainers:junit-jupiter:1.21.4'
    testImplementation project(':code:libraries:test-helpers')
}

